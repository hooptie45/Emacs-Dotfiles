<h1>Deep Elisp Part 1</h1>

<h2>Implementing Monads</h2>

<p>I've started working on an implementation of Kanren, the logic
mini-language covered in <a href="http://tinyurl.com/2hl5bu" title="The Reasoned Schemer">The Reasoned Schemer</a>, in Elisp.  I
want to write about this process visa-vi the unique considerations of
Emacs Lisp as a language, but its a middle sized project, which
depends on several pieces of code, which themselves depend on code
which I haven't described in detail anywhere.  Kanren depends on a
monad (the "stream of substitutions" monad, for the nerds out there).
An implementation of this monad is a good place to start when thinking
about implementing Kanren itself, but I've never written up a piece of
documentation on the implementation of Monads I've cooked up for
Elisp, so I thought I'd start there.</p>

<p>So, this document one part monad tutorial and one part introduction to
the <code>monads.el</code> implementation.  </p>

<p>First, credit where its due: the following implementation of Monads
owes is basic form to the <a href="http://richhickey.github.com/clojure-contrib/monads-api.html" title="Clojure Contrib Monad Library">monad library in
Clojure-contrib</a>.  I wrote it while reading through Jim
Duey's completely excellent <a href="http://intensivesystems.net/writings.html" title="Clojure Monad Tutorials">monad tutorials for
clojure</a>.</p>

<p>Besides minor considerations associated with dealing with the default
dynamic scope in Elisp, the implementation here is basically that of
the Clojure Contrib library.  And I could never have understood what I
was doing without the tutorial.  </p>

<h3>Onto the Monads</h3>

<p>We've reached the monad tutorial part of this document.  Like most
people, I found monads pretty hard to understand when I first
encountered them.  Most monad tutorials speak in Haskell, which I
think can be doubly confusing because its hard to tell exactly where
Haskell ends and where monads begin, since Haskell has special syntax
built in to the language just for the purpose of dealing with monads.
You can, of course, use the functions <code>&gt;&gt;=</code> and <code>return</code> outside of do
notation, as many tutorials do, but at least dealing with <code>&gt;&gt;=</code>
requires a fair amount of comfort with Haskell's infix operator
system.  At least it seemed that way to me. </p>

<p>I really had to implement monads in Elisp to finally get it.  I think
this worked because every part of the monad beastiary had to be built
from the ground up.  If you want  "do notation," for instance, it
isn't just there, you have to write a macro for it, and understand
it.  The uniformity of Lisp syntax (which some people hate, I know)
also helps keep things focused on the ideas.  That is the approach we
will take here.  We'll start just kicking around some general ideas
and gradually build up to <code>do</code> notation, making constant analogy to
well understood lisp and programming concepts.  I think this is better
than throwing oneself in head first. </p>

<h3>Notes on Types</h3>

<p>I think of a monad as a way of decorating function composition.  If I
have a group of functions that conform to a particular input and
output constraint, a monad associated with that constraint tells me
how to combine those functions so that something interesting happens.
The fact that these functions all have the same type of input and
output is critical, otherwise the "plumbing" specified by the monad
won't work.  Such functions are called "monadic functions" and when we
try to understand monads, or a specific monad, we must meditate on the
type of these functions.</p>

<p>When coming to monads from dynamic languages, it can be easy to gloss
over all the type language used to describe them.  But it really helps
to think about types in a loosy-goosey way, at the very least, because
type constraints are what make the plumbing in a monad work.  Monads
generally contain things.  In statically typed languages the monad
type depends on the type of the things it holds, so it would be
specified, but we'll just refer to these things as "values".  Their
type isn't terribly important except that they are distinct from the
type of <em>monadic values</em>. </p>

<p>A <em>monadic value</em> can be thought of as an abstract container which
contains can regular values.  Sometimes this is a rather obvious kind
of relationship.  A list "contains" values, for instance.  Sometimes
it can be much more abstract.</p>

<p>Finally, there are <em>monadic functions</em>.  A monadic function is a
function which takes a regular value and returns a <em>monadic value</em>.
It is important to think about these types while we work on the rest
of the tutorial, so take a second to look at the diagrams, and then
get into the habit of calling to mind the types of various objects
whenever you get confused.  It really helped me.</p>

<p><img src="http://dl.dropbox.com/u/1076954/monadic-types-of-interest.png" alt="Types of Interest" title="Types of Interest" /></p>

<h3>The List or Sequence Monad</h3>

<p>We'll be talking about the sequence monad first.  We will represent
sequences with regular old lisp lists, which can hold values of any
lisp type.  The monadic values, then, are lists.  Lists "contain"
(they also contain) lisp data.  Let us meditate upon the type of
monadic functions in the sequence monad.</p>

<p>==== this space reserved for meditation ==== </p>

<p>If you guessed in your meditation that the monadic functions
associated with the sequence or list monad (we'll use the two
interchangeably from here on) are functions which take a single value,
which is any lisp data, and return a list of lisp-values, you got it.</p>

<p>So, whatever the hell a "list monad" is, its got something to do with
composing functions which take a single lisp value and return a list.  </p>

<p>What kind of functions might do this?  Well, a common idea is that
functions in the list monad represent computations with multiple
possible outcomes or values.  A person might have multiple friends, so
a function <code>get-friends</code> might return a <em>list</em> of persons rather than
a single person.  We might want to go on to calculate those persons'
friends (or enemies, relatives, lovers).  All these <code>get-</code> functions
would be monadic functions in the list monad, and the list monad will
help use deal with them.</p>

<p>Let's start kicking around ideas about composing these functions.  To
do that we need a few functions to consider explicitly.  What are the
simplest monadic functions of the list monad?  Well <code>list</code> is
certainly such a function when you call it with one argument, so lets
put that on the pile.  In that spirit, consider:</p>

<pre><code>(defun stub (x) nil)
(defun list-return (x) (list x))
</code></pre>

<p><img src="http://dl.dropbox.com/u/1076954/monadic-return.png" alt="monadic return" title="monadic return" /></p>

<p><code>list-return</code> can be thought of as a sort of "default" or "simplest
possible" monadic function.  We've just wrapped up <code>list</code> so that it
takes but one argument.  Every monad needs a return, and every return
does something like the above - it wraps its input into the monad in a
simple way.</p>

<p><code>stub</code> is also a monadic function - it ignores its inputs and returns
the empty list.  </p>

<p>Ok, lets write some semi-meaningful list-monadic functions.  Consider:</p>

<pre><code>(defvar *people* '(:ted :lea :leo :james 
                   :harvey :sally :jane :andrew 
                   :catherine) 
                   "A list of all the people that matter.")
(defvar *friends-db* 
      '((:ted (:lea :leo :sally :andrew :catherine :leo :jane))
        (:lea (:ted :leo :jane :andrew :harvey :sally :catherine))
        (:leo (:ted :lea :ted :harvey :sally :jane :andrew 
                    :catherine :harvey :andrew :catherine))
        (:james (:jane :harvey :jane))
        (:harvey (:leo :lea :leo :james :harvey :harvey :sally))
        (:sally (:ted :leo :lea :harvey :jane :andrew))
        (:jane (:lea :leo :james :sally :ted :james :andrew :catherine))
        (:andrew (:ted :lea :leo :sally :jane :leo))
        (:catherine (:ted :leo :lea :leo :jane :catherine :catherin))) 
"Our database of friend connections.")

(defun friends-of (person) 
  "Return a list of all the people in friends-db."
  (alist *friends-db* person)
         ;alist is a function which retrieves
         ;a key's data from an association list.
)

(defun mutual-friends-with (person1)
  "Return a monadic function which returns 
   the friends person1 has in common with person2."
  (lexical-let ((p1-friends (friends-of person1)))
    (lambda (person2) 
      (let ((p2-friends (friends-of person2)))
       (filter (lambda (friend)
                 (in friend p1-friends)) p2-friends)))))
</code></pre>

<p><code>Friends-of</code> is more or less obvious enough.  It simply retrieves
the database entry in the friends database, here represented as an
association list.  It takes a person and returns a list of persons, so
it is a monadic function.  </p>

<p>The second function is a little more complicated.  It takes a person
and returns a function, so its not a monadic function itself.  It
does, however, return a monadic function - one parameterized on
<code>person1</code>.  It returns the friends of <code>person2</code> that are also friends
with <code>person1</code>.  This business of having a function which returns a
<em>parameterized</em> monadic function is pretty common, and it will lend
monadic expressions a certain domain-specific-language feel, as you'll
see.  Note that we need a <code>lexical-let</code> in the function body to create
a closure around <code>p1-friends</code>.  If we used a <code>let</code>, <code>p1-friends</code> would
be out of scope by the time someone called the <code>lambda</code>.  If this is
confusing, see the wikipedia article on <a href="http://en.wikipedia.org/wiki/Scope_(programming)" title="Scope in Programming">scope in programming
languages</a>.  Emacs has a default dynamic scope, but most modern
languages are lexical<sup>2</sup>.   </p>

<p>But we were meditating on fun!tion composition.  Before going any
further lets implement a function which composes functions.  The only
wrinkle here is that order of composition is reversed from the order
of application (<code>f1</code> is applies before <code>f2</code>) because this looks more
natural in an applicative language.</p>

<pre><code>(defun compose2 (f2 f1)
  "Compose F2 and F1 by returning a new function which
   calls F1 on its arguments, then calls F2 on the result"
   (lexical-let ((f1 f1)
                 (f2 f2))
    (lambda (&amp;rest args)
      (funcall f2 (apply f1 args)))))

(defun compose (&amp;rest fs)
  "Compose the functions FS (FN FN-1 FN-2 ... F1) 
   beggining with F1 and working backwards."
  (let ((rfs (reverse fs)))
   (reduce
     (lambda (acc-f f)
       (compose2 f acc-f))
     (cdr rfs)
     :initial-value (car rfs))))
</code></pre>

<p>Let's try just composing some of our list monadic functions.  We might
actually want a function to calculate the <code>friends-of</code> the
<code>friends-of</code> someone:</p>

<pre><code>(compose #'friends-of #'friends-of)
</code></pre>

<p>Because this is Elisp, this doesn't cause an error.  Of course, if we
think about this, we know it can't work.  As soon as well call this
function, we'll get an error, because the second friends-of will be
applied to a list, rather than a single person.  Try it, if you are
having trouble seeing it.</p>

<p>Suppose we were dead set on jamming these two functions together and,
in particular, we want to make sure that the resulting function can be
used wherever either of the two functions might have been used.  That
is, we want the result type to be the same as the input types.  There
are several approaches, obviously, but here is a sort of obvious one
(read the comments here, they are important):</p>

<pre><code>(defun list-func-compose (f2 f1)
   (lexical-let ((f1 f1)
                 (f2 f2))
    (lambda (arg) ; lambda need take only a single arg, as f1 must
      (let* ((r1 (funcall f1 arg)) 
        ; r1 is a list, because it is the result of f1, a monadic function.
        ; f2 accepts regular old values, though, and r1 could
        ; concievably have many values in it.
             (r2s (mapcar f2 r1)))
          ; now r2s contain the results of applying f2 to ALL
          ; the values in r1.  Each of these values in r2s is
          ; itself a list, because f2 always returns a list (it 
          ; is a monadic function itself)
             (reduce #'append r2s)
          ; Well, we want a list of things in the monad, not a
          ; list of lists, so we just append all the r2s together 
          ; (see footnote #1).
          ; Now we've got a list of lisp values, which  means
          ; that this lambda is a monadic function in its own
          ; right.  Success!!
          ))))
</code></pre>

<p>(n.b. If something about <code>r2s</code> type strikes you as odd, see footnote 1.)</p>

<p>Pause to consider what happened here.  We apply <code>f1</code> to the input,
which produces a list (a <em>monadic value</em>).  <code>F2</code> takes values, not
monadic values, so we just <code>mapcar</code> <code>f2</code> over the values in the list.
<code>F2</code>, like <code>f1</code> returns a list, so now we have a list of lists, which
we concatenate into a big list, which is now our output monadic value.</p>

<p>I hate when, in the course of a didactic development, something
springs from nowhere with the proviso that "it will be useful later."
This kind of thing is typical of physics and math texts, and I think
its part of the reason they are so difficult to read.  However, for
the sake of future utility, we are going to factor out a piece of the
above code.  Consider the function <code>list-bind</code> which takes a monadic
value and a monadic function and returns a new monadic value:</p>

<pre><code>(defun list-bind (mon-val mon-fun) ; -&gt; mon-val 
       (let ((results (mapcar mon-fun mon-val)))
          (reduce #'append results)))
</code></pre>

<p>We can rewrite <code>list-func-compose</code> like this, now:</p>

<pre><code>(defun list-func-compose (f2 f1)
  (lexical-let ((f1 f1)
                (f2 f2))
    (lambda (value) 
       (let ((monadic-value (funcall f1 value)))
          (list-bind monadic-value f2)))))
</code></pre>

<p>Ok, what is so special <em>a priori</em> about <code>list-bind</code> (<em>post priori</em>,
its one of the monad functions which any monad needs)?  <code>List-bind</code>
is, in a way, the fundamental operation relating monadic functions and
monadic values in the list monad.  <code>List-func-compose</code> might seem more
directly useful, but it is kind of strange that the resulting function
takes a non-monadic value and returns a monadic one.</p>

<p><code>List-func-compose</code> relates monadic functions and other monadic
functions, but it doesn't reveal much about how they are combined with
values.  <code>List-bind</code> describes that process, and it turns out this is
more interesting.</p>

<p>Another way of thinking of it is that if <code>list</code> describes how to turn
a value into a monadic value, <code>list-bind</code> relates a mondadic function
to a function which both operates on and returns monadic values.</p>

<p><img src="http://dl.dropbox.com/u/1076954/monadic-bind.png" alt="Monadic Bind" title="Monadic Bind" /></p>

<p>Ok, believe it or not, we've basically covered the entire sequence
monad already.  All that is left is to really get the idea and to
understand how it relates to Haskell's do notation and the canonical
monad operations.  But it bears repeating - "do notation" isn't the
same thing as monads.  It is just syntactic sugar.  Its might be best
to understand monads before do notation, depending on your
temperament. </p>

<p>Let's try things out, however.  </p>

<pre><code>(funcall (list-func-compose #'friends-of #'friends-of) :leo) 
(:lea
 :leo :sally :andrew :catherine :leo :jane :ted :leo :jane :andrew
 :harvey :sally :catherine :lea :leo :sally :andrew :catherine :leo
 :jane :leo :lea :leo :james :harvey :harvey :sally :ted :leo :lea
 :harvey :jane :andrew :lea :leo :james :sally :ted :james :andrew
 :catherine :ted :lea :leo :sally :jane :leo :ted :leo :lea :leo
 :jane :catherine :catherin :leo :lea :leo :james :harvey :harvey
 :sally :ted :lea :leo :sally :jane :leo :ted :leo :lea :leo :jane
 :catherine :catherin)
</code></pre>

<p>(We're probably really interested in the unique values of this list.
We could use something called the set monad to get those, or we could 
just pass this result through <code>unique</code>.</p>

<p>Pretty anti-climactic, for sure.  But we can do more interesting
things:</p>

<pre><code>(funcall 
  (list-func-compose 
     (mutual-friends-with :lea) #'friends-of)
  :leo)
</code></pre>

<p>This composition gives a list of the friends of Leo who are also
friends with Lea.  As indicated above, what we've done here is
inserted a parameterized monadic function into the composition.  This
turns out to be useful.  It also hints at the next step.  What if we
wanted to parameterize a monadic function based on some of the values
"coming down the pipe" in the monad?  How could we do that?  In a
regular function composition all those values are invisible.  Is there
way we can name them which preserves the utility of this approach?</p>

<h3>Stepping Back</h3>

<p>Monads in one sentence: a monad is set of operations which relate
specific functions called <em>monadic functions</em> and specific values,
which are either naked <em>values</em> (the input type to <em>monadic
functions</em>) or <em>monadic values</em>, which is the output type of monadic
functions.  </p>

<p>In particular, the <em>bind</em> operation knows how to pull values out of
monadic values, apply monadic functions to them, and collect all the
resulting monadic values into one big monadic value.  Using <em>bind</em> we
can compose or otherwise manipulate monadic functions in a controlled
way.</p>

<p>Everything else is window dressing.</p>

<h3>Window Dressing</h3>

<p>Let's talk about <code>let*</code>.  We've been twisting up our brains into knots
over monads, so let's let those knots untwist and return to this simple,
clean, construct.  In Lisp, variable bindings are explicitly
introduced.  You don't just declare a variable and go, you create a
context for that variable with a <code>let</code> or <code>let*</code> statement.  You
probably know that if you only had <code>lambda</code> and <code>defmacro</code> you could
define <code>let</code> like so:</p>

<pre><code>(defmacro my-let (bindings &amp;rest body)
  (funcall (lambda ,(mapcar #'car bindings)
             ,@body)
      ,@(mapcar #'cadr bindings)))
</code></pre>

<p>eg:</p>

<pre><code>(my-let ((x 1) (y 2)) (+ x y))
</code></pre>

<p>expands to</p>

<pre><code>(funcall (lambda (x y) (+ x y)) 1 2)
</code></pre>

<p>It is clear that x and y can't depend upon one another in this form:</p>

<pre><code>(funcall (lambda (x y) (+ x y)) 1 (+ x 2))
</code></pre>

<p>Obviously won't work (why?).  <code>let*</code> is the form which lets us <em>chain
together</em> multiple variable bindings so that previous bindings are
active during the expression form of subsequent bindings.  Of course
you can implement it as a series of nested <code>let</code>s, but its useful to
write an implementation using only <code>lambda</code>.</p>

<pre><code>(defun empty? (x) (not x)) ; sugar to test if a list is empty.
                           ; nil is the empty list, (not nil) -&gt; t
(defmacro my-let* (bindings &amp;rest body)
  (cond
    ((empty? bindings) `(progn ,@body))
    (t 
     `(funcall (lambda (,(car (car bindings)))
          (my-let* ,(cdr bindings) ,@body))
        ,(cadr (car bindings))))))
</code></pre>

<p>Don't get spooked by this recursive macro.  All it means is that</p>

<pre><code>(my-let* ((x 10)
          (y (+ x 11)))
   (+ x y))
</code></pre>

<p>expands to</p>

<pre><code>(funcall 
      (lambda (x)
        (funcall (lambda (y) (progn (+ x y))) (+ x 11))
     10))
</code></pre>

<p>That is, each subsequent bind value expression is evaluated in the
context of a function where the previous binding symbols are already
bound. </p>

<p>Holy balls!  This is almost function composition!  The <code>lambda</code>
containing our body (the <code>progn</code> form) is <code>f1</code> and each containing
<code>lambda</code> represents a composition onto this inner function.  The only
difference is that we're threading function <em>application</em> through the
composition in such a way as to provide named values into more deeply
nested contexts.  Things are about to get serious!</p>

<p>Also, we are just throwing the word bind around like crazy!  Does this
use of the word bind have something to do with the <code>list-bind</code> function?</p>

<p>You bet it does!  You know, ordinarily I could take or leave Lisp-2's.
It seems kind of crufty to me to have to worry about a second
namespace, to have to remember to type (funcall f-var a b c) or
(funcall #'f a b c) but in this one case, its actually really useful
that we have to write out funcall explicitly.  Its useful because it
reminds us something is going on there.  What would happen if we
replaced those <code>funcall</code>s in the above expansion of <code>let*</code> with some
other function?  Let's kick this can around a bit, and see if
something falls out, shall we?</p>

<pre><code>(defun regular-bind (v f)
  (funcall f v))
</code></pre>

<p>So in the <code>let*</code> expansion funcall is only ever taking functions with
one argument, so the first thing we do is declare a new function which
applies a single argument function to a single value.  Why did we name
it <code>regular-bind</code>?  Well, think about the expression:</p>

<pre><code>(regular-bind 10 (lambda (x) (+ x 1))); -&gt; 11
</code></pre>

<p>In <code>(lambda (x) (+ x 1))</code> <code>x</code> is <em>unbound</em>.  That is what a lambda is,
essentially, a chunk of code in which the arguments are unbound,
waiting to be <em>bound</em> so the body can be evaluated.  <code>Regular-bind</code> is
just a function which <em>binds</em> a value (10, above) to a variable and
executes the code.  Note: <code>funcall</code> takes <code>f</code> and then <code>v</code>, but
<code>regular-bind</code> takes <code>v</code> and then <code>f</code>.  This might make more sense
depending on how you read code ("bind v in f") or it might not.  It is
just convention.  It is called <code>regular-bind</code> because it doesn't
really do anything that funcall doesn't already do.  Monads, though,
are all about <em>irregular</em> bind operations.</p>

<p>Let's rewrite <code>let*</code> so that it uses bind instead of funcall:</p>

<pre><code>(defmacro my-let* (bindings &amp;rest body)
  (cond
    ((empty? bindings) `(progn ,@body))
    (t 
     `(regular-bind
        ,(cadr (car bindings))
        (lambda (,(car (car bindings)))
                      (my-let* ,(cdr bindings) ,@body))))))
</code></pre>

<p>Easy enough.  </p>

<p><code>let*</code> is how you thread together computations in the "identity
monad".  You may have heard someone say something like "regular lisp
lives in the identity monad."  This is what they mean.  Normally, all
bind does is associate a value with a symbol and go.  The above <code>let*</code>
expression now expands to:</p>

<pre><code>(funcall 
      (lambda (x)
        (regular-bind  (+ x 11) (lambda (y) (progn (+ x y)))))
     10)
</code></pre>

<p>You should be chomping at the bit now, because we've basically
invented do notation.  The question you should be asking is "What
happens when we replace bind with some other function?"  Let's make a
slightly newer macro to play with that idea.</p>

<pre><code>(defmacro monadic-let*-inner (bind-symbol binders &amp;rest body)
   (cond 
     ((empty? binders) `(progn ,@body))
     (t 
       (let ((symbol (car (car binders)))
             (bind-expression (cadr (car binders)))
             (leftover-bindings (cdr binders)))
      `(funcall ,bind-symbol 
          ,bind-expression
           (lambda (,symbol)
              (lexical-let ((,symbol ,symbol)) ; create a lexical 
                                               ; over ,symbol.  
                                               ; we want one
                                               ; for lots of monads.
               (monadic-let*-inner 
                   ,bind-symbol ,leftover-bindings ,@body))))))))

(defmacro monadic-let* (bind-f-expression binders &amp;rest body)
  (let ((bind-symbol (gensym "temporary-bind-symbol-")))
    `(let ((,bind-symbol ,bind-f-expression))
          (monadic-let*-inner ,bind-symbol ,binders ,@body))))
</code></pre>

<p>This new form takes an expression which evaluates to a bind function,
a set of let-like binders, and a list of body forms and then it just
builds the same code as a <code>let*</code> except that it uses the specified
<code>bind</code> operation rather than a mere funcall.  <code>Let*</code> sequences
computations.  <code>Monadic-let*</code> sequences operations <em>through a monad,</em>
the behavior of which is specified in <code>bind</code>.  </p>

<p>Let's try this crazy thing out:</p>

<pre><code>(monadic-let* 
       #'list-bind 
       ((x '(1 2 3)) 
        (y (list (+ x 1) (- x 1))))
     (list y)) ; -&gt; (2 0 3 1 4 2)
</code></pre>

<p>Congratulations, friends, we are now living in the list monad.  </p>

<p>Let's think about what this is doing one more time.  We have a series
of expressions (the right hand portion of each binding form).  The
monadic bind operation assumes each expression is a monadic value.  In
our case, these values are lists of lisp data.  Rather than bind the
result of this expression to the symbol on its left directly, our bind
specifies that we should bind the symbol to each value inside the
monad in turn, evaluate the subsequent binding expressions in the same
way, evaluate the body, which must be a monadic value, collect each of
those values, and then, finally, combine them back into a monadic
value again before returning the result.</p>

<h3>That is really complicated</h3>

<h4>Why would anyone do this?</h4>

<p>The reasons for doing something like this are as varied as the kinds
of monads that are out there.  In Haskell, monads are a way of writing
declarative style code in a pure fashion, but even in non-static,
non-pure languages, monads can pull some neat tricks.  The sequence
monad, combined with <code>monadic-let*</code> is essentially a list
comprehension.  Suppose we want to collect all the combinations of two
numbers less than 100 whose sum is less than 10.</p>

<pre><code>(monadic-let*
 #'list-bind 
 ((x (range 1 101))
  (y (range 1 101)))
  (if (&lt; (+ x y)  10) (list (vector x y)) nil))

;-&gt; ((1 1) (1 2) (1 3) (1 4) (1 5) (1 6) (1 7) (1 8) 
       (2 1) (2 2) (2 3) (2 4) ...)
</code></pre>

<p>Here we simply return the empty list when we don't want to put
anything into the monad because appending the empty list onto
something does not change it.  Other applications in an impoverished
language like emacs is to use the continuation monad to fake
co-routines or to use a stream monad to make dealing with lazy lists
easier.  We'll revisit this in another post.</p>

<h3>Notes about the monads.el library</h3>

<p>It turns out its handy in the context of monadic operations to have
<code>bind</code> and <code>return</code> (and some other things) dynamically defined, so in
the <code>monads.el</code> library, <code>monadic-let*</code> is slightly different.  In
that library, monads are defined as hash tables containing <code>:m-bind</code> and
<code>:m-return</code> keys which associate with the appropriate functions.  Eg:</p>

<pre><code>(defvar monad-seq 
  (tbl! 
   :m-zero (list)
   :m-return (lambda (x) (list x))
   :m-bind (lambda (v f) (apply #'append (mapcar f v))))
 "The list/sequence monad.  
  Combine computations over multiple possibilities.")
</code></pre>

<p>Note here that <code>tbl!</code> is is just sugar for <code>make-hash-table</code>.  The
nearest equivalent to what we've called <code>monadic-let*</code> is <code>mlet*_</code>
which takes as its first argument a monad represented as a hash table.
In addition to the bind-chaining described above, it introduces a
dynamic context wherein the functions <code>m-bind</code> and <code>m-return</code> are
defined for that monad.  Because we're in emacs, and everything just
sits in one giant rotting pool of symbols, we prefix the monadic
functions with <code>m-</code> to avoid future name collisions.  The <code>_</code> at the
end distinguishes <code>mlet*_</code> from <code>mlet*</code>.  The only difference between
these forms is that <code>mlet*</code> wraps its <code>body</code> in an implicit <code>m-return</code>
so the result of an <code>mlet*</code> form is always a monadic value.  Sometimes
you want this, sometimes you don't.  So the example above would be
written in the following way using <code>monads.el</code>.</p>

<pre><code>(mlet*_
 monad-seq
 ((x (range 1 101))
  (y (range 1 101)))
 (if (&lt; (+ x y) 10) (m-return (list x y)) nil))
</code></pre>

<p>Note that we can use <code>m-return</code> inside <code>mlet*</code>-like forms, because
they define them automatically.</p>

<p><code>Monads.el</code> defines several common monads in addition to the
sequence monad.  These include the state monad, the continuation
monad, the set monad (parameterized on an equality predicate), an
Error monad, and a Maybe monad.  <code>Monad-parse.el</code> defines a monadic
parser combinator library which is really useful (to me, anyway).</p>

<h3>Sujar</h3>

<p>The monad library can make use of my implementation of Clojure-style
destructuring bind too.  One can write the above expression as</p>

<pre><code>(domonad_ monad-seq 
 [x (range 1 101)
  y (range 1 101)]
 (if (&lt; (+ x y) 10) (m-return (list x y)) nil))
</code></pre>

<p>Besides the superficial change of using a vector for the binding
expressions, you can use arbitrary nested binding forms inside.  If
you are familiar with clojure, it works basically like that, except
that tables are destructured with <code>[:: ]</code> instead of <code>{ }</code> and you can
use table-destructuring on hash tables and alists.  </p>

<h3>Conclusions</h3>

<p>For me, monads didn't hit home until I walked through the process of
creating a new monad from scratch based on my own ideas.  This is
described in <code>weighted-graph-monad.el</code>, but in order to move forward
in our discussion of Kanren, the logic language, we'll need to develop
a stream monad, which facilitates work with lazy lists.  That will be
the next example.</p>

<hr />

<p>Footnote 1: Here is a bit of a confusing thing.  The monadic values of
the list monad are lists of lisp data types.  So a list of lists is,
in fact, a monadic value, which means that <code>r2s</code> above could be
considered, itself, to be a monadic value.  If this didn't occur to
you, just skip the rest of this note.  I bring it up only because if
it has occured to you, it might be somewhat confusing.  You might ask:
why don't we just return <code>r2s</code> instead of <code>(reduce #'append r2s)</code>?
This kind of of conceptual confusion is related to the fact that Lisp
is not statically typed (or is <a href="http://existentialtype.wordpress.com/2011/03/19/dynamic-languages-are-static-languages/" title="Inflammatory Writ">statically typed but you are
restricted to a single type of which the various "types" of values
constitute classes</a>). </p>

<p>In a statically typed language the sequence monad would be
parameterized on the type of its contents, which we'd specify where we
were using it.  So we'd have a type like <code>SeqM Int</code> (read that as "the
sequence of ints monad") which would tell use that our monadic values
were lists of integers <em>only</em>, and consequently <code>r2s</code> above would be
manifestly <em>not</em> a monadic value, and it would be more obvious that we
needed to append its contents together before recapturing a monadic
value.  In a dynamic language this isn't as obvious, but its still
conceptually necessary.  The upshot is that the fact that <code>r2s</code> is a
monadic value is a <em>coincidence</em>, the essence of the list monad is
still to <em>combine</em> the results of monadic functions, rather than to
merely <em>collect</em> them, which is what returning <code>r2s</code> itself would
represent. If you <em>did</em> read this despite my warning, you're probably
really confused.  Its ok.  This stuff is confusing at first.)</p>

<hr />

<p>Footnote 2: This is a long, diversionary footnote.  Probably its best
to finish the rest of the document before reading.</p>

<p>For some reason I find it useful/pleasurable/amusing to consider the
question of monads in languages without scope at all.  Namely, stack
languages like <a href="http://factorcode.org/" title="Factor Language">Factor</a> or <a href="http://www.latrobe.edu.au/philosophy/phimvt/joy/j02maf.html" title="Joy">Joy</a>.  Not only are these languages
without scope, by default, variables are not even named.  Values are
merely pushed onto and pulled off of a stack which is passed
implicitly between "words," the analog of functions:</p>

<pre><code>(require 'with-stack)
(||| 4 4 + 3 -) ; -&gt; 5
</code></pre>

<p>Here we are using a stack language interpreter which lives inside
emacs lisp.  You can get "with-stack.el" from my github.</p>

<p>Without a means of binding variables, "lambdas" become merely
"quotations," simple lists of literals and words which can be executed
by words like <code>call</code>.</p>

<pre><code>(||| 4 '(4 +) call '(3 -) call) ; -&gt; 5
</code></pre>

<p>In the "emacs stack language" quotation serves directly as the quote
operator, and quotations are represented as lists.  Executing a
quotation is the same looping over it, executing each word as it is
encountered, or pushing atoms onto the stack.  </p>

<p>The nakedness of these quotations encourages languages to use them as
blocks in control structures:</p>

<pre><code>(||| 1.0 1&gt;random* .5 &gt; '("true" print) '("false" print) if)
</code></pre>

<p>The above will print "true" 50% of the time and "false" 50% of the
time.  I don't want to get bogged down with a full <code>with-stack</code>
tutorial, but "1.0 1>random*" means "push 1.0 onto the stack and call
the emacs lisp function <code>random*</code> with 1 argument from the stack,
pushing the result".  The word <code>if</code> takes a boolean and two
quotations, executing the first when the bool is true and the second
when it is false.  </p>

<p>Ok, so quotations are lambdas.  What are monadic quotations of the
list monad?  These must be quotations which take a value off the stack
and return a list of values.  The quotation version of <code>return</code> or the
list monad would be:</p>

<pre><code>(||| word: list-return 1&gt;list end:)
</code></pre>

<p>That is, take a value off the stack and return a list with that value
in it.  </p>

<p>What about <code>bind</code>?</p>

<pre><code>(require 'stack-words)
(||| word: list-bind ;( mv f (v ;; mv) ;; mv )
     map '(append) reduce end:)
</code></pre>

<p>Characteristically, the word version is quite terse.  Interestingly,
we hardly need to have an analog of do notation, because variables are
not named and application is merely juxtaposition:</p>

<pre><code>(||| word: bi&gt;list ;( item qtn1 qtn2 ;; ( res1 res2 ) )
     bi 2&gt;list end:)
(||| '(1 2 3) 
     '( '(1 +) '(1 -) bi&gt;list )
     list-bind
     '( '(3 +) '(3 -) bi&gt;list )
     list-bind) ;-&gt; (5 -1 3 -3 6 0 4 -2 7 1 5 -1)
</code></pre>

<p>(The word <code>bi&gt;list</code> takes an item and two quotations, applies the
quotations each to the item and collects the results into a list of
two elements.  It assumes the quotations take one item off and push
one onto the stack.)</p>

<p>Wherever we want to use monadic behavior, we simply replace <code>call</code>
with <code>list-bind</code>.  We can write a word to make this more pleasant:</p>

<pre><code>(||| word: fold-bind-into ; ( qtnseq bind-qtn ;; qtnseq )
      '( curry ) curry map 1&gt;flatten-once end:)
(||| word: monadically ; ( init-val qtnseq bind-qtn ;; result )
       fold-bind-into call end:)

(||| '(1 2 3)
       {{ '( '(1 +) '(1 -) bi&gt;list )
          '( '(3 +) '(3 -) bi&gt;list ) }}
     '(list-bind) monadically )
</code></pre>

<p>Where <code>{{</code> is a parsing word which makes a list of what is on the
stack between <code>{{</code> and <code>}}</code>.  We could have used quote to make the
list of quotations, but we'd be getting a lot of quote marks on the
screen.  All this word does is fold the contents of a binding
quotation (here a simple call to <code>list-bind</code>) into the sequence of
quotations we want to operate monadically.  Then we simply call the
resulting quotation.  </p>

<p>Assuming some comfort with the idioms of concatenative languages, the
result is an incredibly simple demonstration of the essence of monadic
operation.  It strikes me that it is often the lowest-level words
which are the hardest to read and write in concatenative languages,
while high level code has an almost comic simplicity.  I wonder what
this means.  I wonder what this means?</p>

<hr />
